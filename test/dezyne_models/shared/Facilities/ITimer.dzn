// ITimer
//
// Description:
// - Timing service
// - Stateful interface
// - Exclusive resource, each client must create his own instance
// - Makes use of the Timing facility in Dezyne Pump, therefore cancelling
//   a pending timer is possible without a sentinel event.
//
// IMPORTANT: A single instance of this model must _not_ be shared amongst other
//            components. Do not inject into the Dezyne locator.
//

import Types.dzn;

interface ITimer
{
in void Create(in MilliSeconds waitingTimeMs);
in void Cancel();
out void Timeout();

behaviour
{
    bool running = false;

    [!running]
    {
        on Create: running = true;
        on Cancel: {}
    }

    [running]
    {
        on Cancel: running = false;
        on inevitable: { Timeout; running = false; }
    }

} // behaviour
} // interface
